TITLE DEMO: Sub-circuit (TG)

***** INVERTER *****
.SUBCKT inv GND IP OP VDD
MM1 OP IP GND GND NMOS_VTG W=90n L=50n m=1
MM0 OP IP VDD VDD PMOS_VTG W=119n L=90n m=1
.ENDS
***** TG *******
.SUBCKT TG C GND IP OP VDD
XInv GND C net17 VDD / inv
MMP IP net17 OP VDD PMOS_VTG W=300n L=50n m=1
MMN IP C OP GND NMOS_VTG W=100n L=50n m=1
.ENDS
***** Process and Temperature *****
.PROTECT $ keep models private (so that *.lis file will not contain the information of spice model)
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.UNPROTECT
***************************
Xtg C GND IP OP VDD TG
***** Parameter *****
.PARAM VSUP = 1.0V
*********************

***** Supply and Stimuli *****
VVDD VDD GND VSUP
VC C GND PULSE(0V VSUP 0n 0.1n 0.1n 9n 20n) 
VIP IP GND PULSE(0V VSUP 0.2n 0.1n 0.1n 4n 10n)
******************************

***** Command Options *****
.OP              $ Print operating point analysis data to *.lis file
.OPTION POST     $ Generate graph file (*.tr#, *.ms#, *.ac#)
.OPTION ACCURATE $ Higher simulation accuracy
.OPTION CAPTAB   $ Print node capacitance
.OPTION PROBE
***************************

***** Analysis *****
.TRAN 0.001n 100n 
.PROBE TRAN V(*)
********************

*****Delta time *****
.MEAS TRAN Tplh TRIG V(IP) VAL='1*0.5' TD=20n Rise=1 
+		TARG V(OP) VAL='1*0.5' TD=20n Rise=1
.MEAS TRAN Tphl TRIG V(IP) VAL='1*0.5' TD=20n Fall=1 
+		TARG V(OP) VAL='1*0.5' TD=20n Fall=1

.ALTER
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.ALTER
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' SS_VTG

.END $ End-of-File
