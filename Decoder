TITLE DEMO: Decoder

***** DECODER *****

.SUBCKT inv GND IN OUT VDD
MM1 OUT IN GND GND NMOS_VTG W=90n L=50n m=1
MM0 OUT IN VDD VDD PMOS_VTG W=119n L=90n m=1
.ENDS

************************************************************************
.SUBCKT AND2 GND IA IB OP VDD
MM3 net14 IB GND GND NMOS_VTG W=90n L=50n m=1
MM2 net10 IA net14 net14 NMOS_VTG W=90n L=50n m=1
MM5 OP net10 GND GND NMOS_VTG W=90n L=50n m=1
MM0 net10 IB VDD VDD PMOS_VTG W=100n L=50n m=1
MM1 net10 IA VDD VDD PMOS_VTG W=100n L=50n m=1
MM4 OP net10 VDD VDD PMOS_VTG W=119n L=50n m=1
.ENDS

************************************************************************
.SUBCKT Decoder A0 A1 D0 D1 D2 D3 GND VDD
XI7 GND net16 net17 D0 VDD / AND2
XI8 GND A0 net17 D1 VDD / AND2
XI9 GND A1 net16 D2 VDD / AND2
XI10 GND A1 A0 D3 VDD / AND2
Xnot2 GND A1 net17 VDD / inv
Xnot1 GND A0 net16 VDD / inv
.ENDS
***** Process and Temperature *****

.PROTECT $ keep models private (so that *.lis file will not contain the information of spice model)
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.UNPROTECT

*******************************************************
Xunit A0 A1 D0 D1 D2 D3 GND VDD Decoder
***** Parameter *****
.PARAM VSUP = 1.0V
*********************
***** Supply and Stimuli *****
VVDD VDD GND VSUP
VA0 A0 GND PULSE(0V VSUP 0n 0.1n 0.1n 9n 20n) 
VA1 A1 GND PULSE(0V VSUP 4n 0.1n 0.1n 9n 20n)
******************************

***** Command Options *****
.OP              $ Print operating point analysis data to *.lis file
.OPTION POST     $ Generate graph file (*.tr#, *.ms#, *.ac#)
.OPTION ACCURATE $ Higher simulation accuracy
.OPTION CAPTAB   $ Print node capacitance
.OPTION PROBE
***************************
***** Analysis *****
.TRAN 0.001n 100n 
.PROBE V(*) 
.END $ End-of-File
