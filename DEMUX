.TITLE DEMO: DEMUX
************************************************************************

.SUBCKT AND3 GND IA IB IC OP VDD
*.PININFO IA:I IB:I IC:I OP:O GND:B VDD:B
MM5 net18 IC GND GND NMOS_VTG W=90n L=50n m=1
MM3 net7 IA net19 net19 NMOS_VTG W=90n L=50n m=1
MM4 net19 IB net18 net18 NMOS_VTG W=90n L=50n m=1
MM6 OP net7 GND GND NMOS_VTG W=90n L=50n m=1
MM1 net7 IB VDD VDD PMOS_VTG W=100n L=50n m=1
MM0 net7 IA VDD VDD PMOS_VTG W=100n L=50n m=1
MM2 net7 IC VDD VDD PMOS_VTG W=100n L=50n m=1
MM7 OP net7 VDD VDD PMOS_VTG W=119n L=50n m=1
.ENDS
************************************************************************

.SUBCKT inv GND IN OUT VDD
*.PININFO IN:I OUT:O GND:B VDD:B
MM1 OUT IN GND GND NMOS_VTG W=90n L=50n m=1
MM0 OUT IN VDD VDD PMOS_VTG W=119n L=90n m=1
.ENDS
************************************************************************

.SUBCKT Demux D0 D1 D2 D3 GND IP S0 S1 VDD
*.PININFO IP:I S0:I S1:I D0:O D1:O D2:O D3:O GND:B VDD:B
XI3 GND IP S1 S0 D3 VDD / AND3
XI2 GND IP S1_bar S0_bar D0 VDD / AND3
XI1 GND IP S1_bar S0 D1 VDD / AND3
XI0 GND IP S1 S0_bar D2 VDD / AND3
XI5 GND S0 S0_bar VDD / inv
XI4 GND S1 S1_bar VDD / inv
.ENDS
***** Process and Temperature *****

.PROTECT $ keep models private (so that *.lis file will not contain the information of spice model)
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.UNPROTECT

*******************************************************
Xunit D0 D1 D2 D3 GND IP S0 S1 VDD Demux
***** Parameter *****
.PARAM VSUP = 1.0V
*********************
***** Supply and Stimuli *****
VVDD VDD GND VSUP
VS0 S1 GND PULSE(0V VSUP 1n 0.1n 0.1n 9n 20n) 
VS1 S0 GND PULSE(0V VSUP 4n 0.1n 0.1n 9n 20n)
VIP IP GND 1V
******************************

***** Command Options *****
.OP              $ Print operating point analysis data to *.lis file
.OPTION POST     $ Generate graph file (*.tr#, *.ms#, *.ac#)
.OPTION ACCURATE $ Higher simulation accuracy
.OPTION CAPTAB   $ Print node capacitance
***************************
***** Analysis *****
.TRAN 0.001n 100n 
.END $ End-of-File
