TITLE DEMO: Sub-circuit (XNOR)

***** INVERTER *****
.SUBCKT inv GND IN OP VDD
MMD OP IN GND GND NMOS_VTG W=90n L=50n m=1
MMU OP IN VDD VDD PMOS_VTG W=119n L=50n m=1
.ENDS

*****  *******
.SUBCKT XNOR GND IA IB OP VDD
MMP net9 IA VDD VDD PMOS_VTG W=300n L=50n m=1
MM1 OP IB net9 VDD PMOS_VTG W=300n L=50n m=1
MM2 OP IA_bar net8 VDD PMOS_VTG W=300n L=50n m=1
MM0 net8 IB_bar VDD VDD PMOS_VTG W=300n L=50n m=1
XI0 GND IA IA_bar VDD / inv
XI1 GND IB IB_bar VDD / inv
MMN OP IA_bar net7 GND NMOS_VTG W=100n L=50n m=1
MM3 OP IB_bar net6 GND NMOS_VTG W=100n L=50n m=1
MM5 net6 IA GND GND NMOS_VTG W=100n L=50n m=1
MM4 net7 IB GND GND NMOS_VTG W=100n L=50n m=1
.ENDS

***** Process and Temperature *****
.PROTECT $ keep models private (so that *.lis file will not contain the information of spice model)
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.UNPROTECT
***************************
Xunit GND IA IB OP VDD XNOR
***** Parameter *****
.PARAM VSUP = 1.0V
*********************
***** Supply and Stimuli *****
VVDD VDD GND VSUP
VIA IA GND PULSE(0V VSUP 0n 0.1n 0.1n 9n 20n) 
VIB IB GND PULSE(0V VSUP 2n 0.1n 0.1n 9n 20n)
******************************

***** Command Options *****
.OP              $ Print operating point analysis data to *.lis file
.OPTION POST     $ Generate graph file (*.tr#, *.ms#, *.ac#)
.OPTION ACCURATE $ Higher simulation accuracy
.OPTION CAPTAB   $ Print node capacitance
.OPTION PROBE
***************************

***** Analysis *****
.TRAN 0.001n 100n 
.PROBE TRAN V(*)
********************

*****Delta time *****
.MEAS TRAN Tplh TRIG V(IB) VAL='1*0.5' TD=20n Rise=1 
+		TARG V(OP) VAL='1*0.5' TD=20n Rise=1
.MEAS TRAN Tphl TRIG V(IA) VAL='1*0.5' TD=20n Fall=1 
+		TARG V(OP) VAL='1*0.5' TD=20n Fall=2

.ALTER
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.ALTER
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' SS_VTG

.END $ End-of-File
