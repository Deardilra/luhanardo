TITLE DEMO: Lab2 Buffer

***** Inverter *****
.SUBCKT INV VIN OUT VDD GND 
MMPD OUT VIN GND GND NMOS_VTG W=90n L=52.5n M=1
MMPU OUT VIN VDD VDD PMOS_VTG W=132.5n L=52.5n M=1
.ENDS

***** Process and Temperature *****
.PROTECT $ keep models private (so that *.lis file will not contain the information of spice model)
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.UNPROTECT
.TEMP 25
***************************

***** Parameter *****
.PARAM VSUP = 1.0V
*********************

***** Call Sub-circuits *****
XINV1 VIN BUF VDD GND INV
XINV2 BUF OUT VDD GND INV
******************.***********

***** Supply and Stimuli *****
VVDD VDD GND VSUP
VVIN VIN GND PULSE(0V VSUP 0n 0.1n 0.1n 4.9n 10n)
******************************

***** Command Options *****
.OP              $ Print operating point analysis data to *.lis file
.OPTION POST     $ Generate graph file (*.tr#, *.ms#, *.ac#)
.OPTION ACCURATE $ Higher simulation accuracy
.OPTION CAPTAB   $ Print node capacitance
***************************

***** Analysis *****
.TRAN 0.01n 100n 
***** VMAX, VMIN, VPP *****
.MEAS TRAN MAX_OUT MAX V(OUT) FROM=15n TO=100n 
.MEAS TRAN MIN_OUT MIN V(OUT) FROM=15n TO=100n 
.MEAS TRAN VPP_OUT PARAM='MAX_OUT-MIN_OUT'    

******Rise time**************
.MEAS TRAN TR_OUT TRIG V(OUT) VAL='VPP_OUT*0.1' TD=10n RISE=1 $ Rise time measurement of the signal 'OUT'
+									TARG V(OUT) VAL='VPP_OUT*0.9' TD=10n RISE=1
***** Fall time *****
.MEAS TRAN TF_OUT TRIG V(OUT) VAL='VPP_OUT*0.9' TD=10n Fall=1 $ Fall time measurement of the signal 'OUT'
+									TARG V(OUT) VAL='VPP_OUT*0.1' TD=10n Fall=1  
*****************************
.ALTER
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.ALTER
.TEMP -40
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' SS_VTG
.TEMP 125
.END $ End-of-File
