TITLE DEMO: Sub-circuit (XOR)

***** INVERTER *****
.SUBCKT inv GND IN OUT VDD
MM1 OUT IN GND GND NMOS_VTG W=90n L=50n m=1
MM0 OUT IN VDD VDD PMOS_VTG W=119n L=90n m=1
.ENDS
*****  *******
.SUBCKT XOR GND IA IB OP VDD
XI0 GND IA IA_bar VDD / inv
XI1 GND IB IB_bar VDD / inv
MMN OP IA net52 GND NMOS_VTG W=100n L=50n m=1
MM3 OP IA_bar net50 GND NMOS_VTG W=100n L=50n m=1
MM5 net50 IB_bar GND GND NMOS_VTG W=100n L=50n m=1
MM4 net52 IB GND GND NMOS_VTG W=100n L=50n m=1
MMP net51 IB VDD VDD PMOS_VTG W=100n L=50n m=1
MM1 OP IA_bar net51 VDD PMOS_VTG W=100n L=50n m=1
MM2 OP IB_bar net49 VDD PMOS_VTG W=100n L=50n m=1
MM0 net49 IA VDD VDD PMOS_VTG W=100n L=50n m=1
.ENDS
***** Process and Temperature *****
.PROTECT $ keep models private (so that *.lis file will not contain the information of spice model)
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.UNPROTECT
***************************
Xunit GND IA IB OP VDD XOR
***** Parameter *****
.PARAM VSUP = 1.0V
*********************
***** Supply and Stimuli *****
VVDD VDD GND VSUP
VIA IA GND PULSE(0V VSUP 0n 0.1n 0.1n 9n 20n) 
VIB IB GND PULSE(0V VSUP 4n 0.1n 0.1n 9n 20n)
******************************

***** Command Options *****
.OP              $ Print operating point analysis data to *.lis file
.OPTION POST     $ Generate graph file (*.tr#, *.ms#, *.ac#)
.OPTION ACCURATE $ Higher simulation accuracy
.OPTION CAPTAB   $ Print node capacitance
.OPTION PROBE
***************************

***** Analysis *****
.TRAN 0.001n 100n 
.PROBE TRAN V(*)
********************

*****Delta time *****
.MEAS TRAN Tplh TRIG V(IA) VAL='1*0.5' TD=20n Rise=1 
+		TARG V(OP) VAL='1*0.5' TD=20n Rise=1
.MEAS TRAN Tphl TRIG V(IB) VAL='1*0.5' TD=20n Rise=1 
+		TARG V(OP) VAL='1*0.5' TD=20n Fall=1

.ALTER
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' TT_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.ALTER
.DEL LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' FF_VTG
.LIB '/misc/vlsicollege-data/techfile/course_college_vlsi/FreePDK45/ncsu_basekit/models/hspice/FreePDK45.l' SS_VTG

.END $ End-of-File
